apply plugin: 'com.android.application'

ext {
    autoVersionCode = getVersionCode()
}

// VERSION_CODE reader
def getVersionCode() {
    def versionFile = file("${rootDir}/version.properties")
    def defaultCode = 1000

    if (!versionFile.exists()) {
        println "version.properties not found. Using default VERSION_CODE=${defaultCode}"
        return defaultCode
    }

    def Properties props = new Properties()
    versionFile.withInputStream { props.load(it) }
    def codeProp = props['VERSION_CODE']

    if (!codeProp) {
        println "VERSION_CODE not set in version.properties. Using default VERSION_CODE=${defaultCode}"
        return defaultCode
    }

    def code = codeProp.toInteger()
    println "Building with VERSION_CODE: ${code}"
    return code
}

android {
    namespace "com.tarotgarden.app"
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.tarotgarden.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode project.ext.autoVersionCode
        versionName "1.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        debug {
            // applicationIdSuffix 제거 - Google Play 버전과 같은 패키지명 사용
            // applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            debuggable true
        }
    }

    // 빌드 타입별 출력 파일명 설정
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "release") {
                // 릴리즈 APK만 파일명 변경
                outputFileName = "tarot-garden-release-${variant.versionName}-${variant.versionCode}.apk"
            }
            // 디버그는 기본 파일명 유지 (app-debug.apk)
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation 'com.android.billingclient:billing:6.2.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
